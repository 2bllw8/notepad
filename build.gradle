/*
 * Copyright (c) 2021 2bllw8
 * SPDX-License-Identifier: GPL-3.0-only
 */
buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath('com.android.tools.build:gradle:8.13.0')
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

tasks.register("clean", Delete) {
    delete rootProject.layout.buildDirectory
}

final Properties localProps = new Properties()
if (file("local.properties").exists()) {
    localProps.load(file("local.properties").newDataInputStream())
}

ext {
    minSdkVersion = 27
    targetSdkVersion = 36

    versionCode = 5
    versionName = "2.3"

    namespace = "exe.bbllw8.notepad"

    sourceCompatibilityVersion = JavaVersion.VERSION_21
    targetCompatibilityVersion = JavaVersion.VERSION_21

    keyStoreFile = localProps.getProperty("androidStoreFile")
    if (keyStoreFile != null && keyStoreFile != "") {
        keyStoreFile = file(keyStoreFile)
    }
    keyStorePassword = localProps.getProperty("androidStorePassword")
    keyAlias = localProps.getProperty("androidKeyAlias")
    keyPassword = localProps.getProperty("androidKeyPassword")
}
