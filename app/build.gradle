/*
 * Copyright (c) 2021 2bllw8
 * SPDX-License-Identifier: GPL-3.0-only
 */
plugins {
    id("com.android.application")
}

android {
    namespace = rootProject.ext.namespace
    compileSdk = rootProject.ext.targetSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }

    buildFeatures {
        buildConfig = false
    }

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    dependenciesInfo {
        includeInApk = false
    }

    signingConfigs {
        if (rootProject.ext.keyStoreFile != null && rootProject.ext.keyStoreFile.exists()) {
            notepad {
                storeFile rootProject.ext.keyStoreFile
                storePassword rootProject.ext.keyStorePassword
                keyAlias rootProject.ext.keyAlias
                keyPassword rootProject.ext.keyPassword
            }
        }
    }

    buildTypes {
        final def useNotepadConfig = rootProject.ext.keyStoreFile != null
                && rootProject.ext.keyStoreFile.exists()

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"

            if (useNotepadConfig) {
                signingConfig = signingConfigs.named("notepad").get()
            }
        }
        debug {
            if (useNotepadConfig) {
                signingConfig = signingConfigs.named("notepad").get()
            }
        }
    }
}

dependencies {
    final def annotation_version = "1.3.0"
    final def either_version = "3.4.0"

    implementation "androidx.annotation:annotation:$annotation_version"
    implementation "io.github.2bllw8:either:$either_version"
}
